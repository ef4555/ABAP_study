# 8/18 과제 프로그램

생성자: 최예훈
생성 일시: 2023년 8월 18일 오후 5:11
태그: 실습

필드심볼로 바꾸고

레이아웃 선택할 수 있게 만듦

TOP-OF-PAGE로 수정

```abap
*&---------------------------------------------------------------------*
*& Report  ZPPR_BOM_YH
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

REPORT  zppr_bom_yh02.

INCLUDE zppr_bom_yh02_top.
INCLUDE zppr_bom_yh02_cls.
INCLUDE zppr_bom_yh02_scr. " 1000번 스크린(조회 조건 화면)
INCLUDE zppr_bom_yh02_pbo.
INCLUDE zppr_bom_yh02_pai.
INCLUDE zppr_bom_yh02_frm. " 서브루틴 정리

*----------------------------------------------------------------------*
* INITIALIZATION                                                       *
*----------------------------------------------------------------------*
INITIALIZATION.
  PERFORM init_before_run. " 초기값을 넣어주어야 할 때 사용

*----------------------------------------------------------------------*
* START OF SELECTION                                                   *
*----------------------------------------------------------------------*
START-OF-SELECTION.
  PERFORM select_data. " 조회값에 기반하여 BOM 정보 불러옴

*----------------------------------------------------------------------*
* END OF SELECTION                                                     *
*----------------------------------------------------------------------*
END-OF-SELECTION.
  CALL SCREEN 0100. " 조회 결과 ALV 를 담을 화면 100번 불러옴
```

```abap
*----------------------------------------------------------------------*
*       CLASS lcl_event DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_event DEFINITION. "정의부
  PUBLIC SECTION.
    METHODS : top_of_page
      FOR EVENT top_of_page OF cl_gui_alv_grid
      IMPORTING e_dyndoc_id.
ENDCLASS.                    "lcl_event DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_event IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_event IMPLEMENTATION. "구현부
  METHOD top_of_page.
    PERFORM set_top_page USING e_dyndoc_id.
  ENDMETHOD.                    "top_of_page
ENDCLASS.                    "lcl_event IMPLEMENTATION

DATA : g_handler TYPE REF TO lcl_event.
```

```abap
*&---------------------------------------------------------------------*
*&  Include           ZPPR_BOM_YH02_SCR
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&  Include           ZPPR_BOM_YH_SCR
*&---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK sel_01 WITH FRAME TITLE text-001.

PARAMETERS : p_datuv LIKE stko-datuv DEFAULT sy-datum, " 효력 시작일
             p_werks LIKE marc-werks OBLIGATORY DEFAULT '1300'. " 플랜트

SELECT-OPTIONS :  s_matnr FOR mara-matnr VISIBLE LENGTH 10, " 자재코드
                  s_stlan FOR mast-stlan VISIBLE LENGTH 10, " 용도
                  s_stlal FOR mast-stlal. " 대체 BOM "

PARAMETERS :  p_mtart LIKE mara-mtart DEFAULT 'FERT'. " 자재 유형

SELECTION-SCREEN END OF BLOCK sel_01.

SELECTION-SCREEN BEGIN OF BLOCK sel_02 WITH FRAME TITLE text-002.

  PARAMETERS : p_vari like disvariant-variant.

SELECTION-SCREEN END OF BLOCK sel_02.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  gs_variant-report = sy-repid.
  call FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      is_variant = gs_variant
      i_save = 'A'
    IMPORTING
      es_variant = gs_variant
    EXCEPTIONS
      not_found = 1
      program_error = 2
      OTHERS = 3.

IF sy-subrc = 0.
  p_vari = gs_variant-variant.
ENDIF.
```

```abap
*&---------------------------------------------------------------------*
*&  Include           ZPPR_BOM_YH02_PBO
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Module  STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*

MODULE STATUS_0100 OUTPUT.
  SET PF-STATUS 'B100'.
  SET TITLEBAR 'YHTITLE'.
ENDMODULE.                 " STATUS_0100  OUTPUT

MODULE set_alv OUTPUT.
  PERFORM alv_obj. " 컨테이너와 그리드 생성
  PERFORM alv_layout_setting. " ALV 레이아웃 설정
  PERFORM set_fieldcat. " 필드 카탈로그 설정
  PERFORM alv_display. " ALV 출력
ENDMODULE.                 " SET_ALV  OUTPUT
```

```abap
*----------------------------------------------------------------------*
***INCLUDE ZPPR_BOM_YH02_PAI .
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_0100  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE user_command_0100 INPUT.
  DATA : lv_answer TYPE c.

  CASE ok_code.
    WHEN 'BACK' OR 'CANC'.
      LEAVE TO SCREEN 0. " 이전 화면으로 가세옹
    WHEN 'EXIT'.
      CALL FUNCTION 'POPUP_TO_CONFIRM_step' " 데아터의 손실 가능성이 있는 작업을 수행할 때 사용되는 함수
        EXPORTING
          textline1 = '프로그램을 종료하시겠습니까?' " 팝업창의 본문 내용
          start_column        = 30 " 팝업창 위치
          start_row           = 6
          defaultoption       = 'Y' " 디폴트 설정 값을 Y 값으로 한다.
          titel     = '프로그램 종료' " 팝업창의 제목
        IMPORTING
          answer    = lv_answer. "팝업창에 입력한 값을 저장

      check lv_answer ne 'N'. " N을 입력하지 않을 시 = Y를 입력했을 시
        LEAVE PROGRAM. " 프로그램 종료

     WHEN 'SAVE'.
       MESSAGE '데이터 저장 테스트' TYPE 'I'.
    WHEN OTHERS.
  ENDCASE.
ENDMODULE.                 " USER_COMMAND_0100  INPUT
```

```abap
*&---------------------------------------------------------------------*
*&  Include           ZPPR_BOM_YH02_FRM
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM select_data.

  PERFORM init_data.
  PERFORM get_werks_name. " 작업장 이름 불러옴
  PERFORM sel_bom_data. " 조건값에 맞는 BOM 정보 조회
  PERFORM loading. " BOM 정보를 조회중입니다 표시
  PERFORM msg. " 조회중 메시지 출력

  LOOP AT it_base ASSIGNING <it_base_fs>. " INTO it_base_str. " 입력받은 자재번호 range를 순회하면서 조회
    PERFORM bom_expl. " 하위 BOM 조회
  ENDLOOP.

ENDFORM.                    " SELECT_DATA
*&---------------------------------------------------------------------*
*&      Form  INIT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM init_data .
  CLEAR :  con1_dock, g_grid1,
           gs_variant, gs_layout,
           gs_fieldcat, gt_fieldcat,
           it_list, it_list[],
           it_list2, it_list2[],
           it_base, it_base_str.

ENDFORM.                    " INIT_DATA

*&---------------------------------------------------------------------*
*&      Form  get_werks_name
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_werks_name .
  SELECT name1 INTO CORRESPONDING FIELDS OF t001w
    FROM t001w
    WHERE werks = p_werks.
  ENDSELECT.
ENDFORM.                    " GET_WERKS_NAME
* 사용할 인터널 테이블, 구조체 초기화
FORM init_before_run.

ENDFORM.                    "INIT_BEFORE_RUN

*&---------------------------------------------------------------------*
*&      Form  loading
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM loading.

  DATA : lv_text(50) TYPE c.

  lv_text = 'BOM 정보를 조회중입니다... '.

  CALL FUNCTION
    'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      text   = lv_text
    EXCEPTIONS
      OTHERS = 0.

ENDFORM.                    "loading

*&---------------------------------------------------------------------*
*&      Form  sel_bom_data
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM sel_bom_data .
  SELECT b~matnr c~maktx a~werks a~stlan a~stlal a~stlnr INTO CORRESPONDING FIELDS OF TABLE it_base
    FROM mast AS a INNER JOIN mara AS b
                       ON a~matnr = b~matnr
                 INNER JOIN makt AS c
                       ON a~matnr = c~matnr
    WHERE a~werks = p_werks
        AND a~matnr IN s_matnr " 입력받은 자재코드
        AND a~stlan IN s_stlan " 입력받은 용도
        AND a~stlal IN s_stlal " 입력받은 대체 BOM
        AND b~mtart = p_mtart  " 입력받은 자재 유형
        AND c~spras = sy-langu. " 현재 시스템 언어

  DELETE ADJACENT DUPLICATES FROM it_base COMPARING stlnr. "  stlnr(BOM)기준으로 중복 제거
  SORT it_base BY matnr. " matnr 기준으로 정렬

ENDFORM.                    " SEL_BOM_DATA
*&---------------------------------------------------------------------*
*&      Form  MSG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM msg .
  IF it_base IS NOT INITIAL.. " 인터널 테이블이 비어있지 않을 경우 = 조회값이 있을경우
    MESSAGE '정상 조회되었습니다.' TYPE 'S'.

  ELSE.
    MESSAGE '조회할 데이터가 없습니다.' TYPE 'I'. " 조회값이 없습니다 팝업 출력
    LEAVE LIST-PROCESSING. " 리스트 처리를 중단하고 빠져나옴(조회 페이지로 다시 감)
  ENDIF.
ENDFORM.                    " MSG
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  BOM_EXPL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM bom_expl.
  DATA : it_stpox TYPE TABLE OF stpox WITH HEADER LINE WITH NON-UNIQUE KEY idnrk, " BOM EXPL 결과 저장할 인터널 테이블
         ls_topmat TYPE cstmat, " 최상위 자재 정보 저장할 구조체
         it_cscmat TYPE TABLE OF cscmat WITH HEADER LINE. " BOM 카테고리를 저장할 인터널 테이블

  CLEAR : it_list, it_list[], it_stpox, it_stpox[], ls_topmat, it_cscmat, it_cscmat[]. " 서브루틴에서 사용할 테이블들, 구조체들 초기화.

  CALL FUNCTION 'CS_BOM_EXPL_MAT_V2'
    EXPORTING
      capid                 = 'PP01' " 어플리케이션
      datuv                 = p_datuv " 효력시작일
      emeng                 = 0
      mehrs                 = 'X' " 다중 BOM 지시자 - 없으면 단일 레벨만 읽는다
      mtnrv                 = <it_base_fs>-matnr " 상위 자재 번호(모자재)
      werks                 = p_werks " 플랜트
      stlan                 = <it_base_fs>-stlan " BOM 용도
      stlal                 = <it_base_fs>-stlal " 대체 BOM
    IMPORTING
      topmat                = ls_topmat " 최상위 자재 번호를 저장
    TABLES
      stb                   = it_stpox[] " BOM EXPL 결과 저장
      matcat                = it_cscmat[] " 자재 카테고리
    EXCEPTIONS
      alt_not_found         = 1
      call_invalid          = 2
      missing_authorization = 4
      no_bom_found          = 5
      no_plant_data         = 6
      no_suitable_bom_found = 7
      conversion_error      = 8
      OTHERS                = 9.

* 4에서 취득한 it_stpox 기준으로 for all entries in 사용 하여, 제품 기준으로 하위 품목을 가지고 온다.
* it_list = 가져온 하위 품목이 담긴 리스트
  SELECT ma~matnr kt~maktx
      INTO CORRESPONDING FIELDS OF TABLE it_list
      FROM mara AS ma INNER JOIN marc AS mc
                             ON mc~matnr = ma~matnr
                      INNER JOIN makt AS kt
                             ON ma~matnr = kt~matnr
    FOR ALL ENTRIES IN it_stpox
    WHERE ma~matnr = it_stpox-idnrk
    AND kt~spras = sy-langu.

  LOOP AT it_stpox. " BOM EXPL에서 얻은 하위 자재 정보 순회하면서
    READ TABLE it_list WITH KEY matnr = it_stpox-idnrk. " 자재코드를 KEY로 하여 구성 부품 정보 가져와서 it_list의 정보와 매칭하여 it_list의 헤더 라인에 정보를 담는다.

    " 이후 이 it_list 헤더라인을 원하는 정보를 담아서 수정
    it_list-werks = <it_base_fs>-werks. " 최상위 모품목 작업장
    it_list-matnr = <it_base_fs>-matnr. " 최상위 모품목 자재코드
    it_list-maktx = <it_base_fs>-maktx. " 최상위 모품목 이름
    it_list-stlan = <it_base_fs>-stlan. " 최상위 모품목 용도
    it_list-stlal = <it_base_fs>-stlal. " 최상위 모품목 대체 BOM
    it_list-stlst = ls_topmat-stlst. " 조회 제품의 대체 BOM
    it_list-stufe = it_stpox-stufe.    " 구성부품 레벨
    it_list-ojtxp = it_stpox-ojtxp.    " 구성부품 이름

    READ TABLE it_cscmat WITH KEY index = it_stpox-ttidx. " 자재 카테고리(it_cscmat)에서 BOM 단계(INDEX: 범주 테이블 내 인덱스)를 키로 하여 모품목의 자재코드를 가져옴
    it_list-matnr_m = it_cscmat-matnr. " 구성부품의 그 위의 모품목 자재코드
    it_list-matnr_sub = it_stpox-idnrk. " 구성부품의 자재코드
    it_list-menge = it_stpox-mngko.    " 구성부품수량
    it_list-meins = it_stpox-meins.    " 구성부품 단위
    it_list-mtart = it_stpox-mtart.    " 구성부품 용도
    it_list-datuv = it_stpox-datuv.    " 구성부품 효력 시작일
    it_list-stlal = it_stpox-stlal.    " 구성부품 대체 BOM
    it_list-ausch = it_stpox-ausch.    " 구성 부품 스크랩
    it_list-sanka = it_stpox-sanka.    " 원가 계산 관련 지시자

* 1레벨의 경우에만 기준수량을 가져온다."최상위 인경우 bom 헤더 수량을 가지고 온다
    IF it_list-stufe EQ 1.
      it_list-bmeng = ls_topmat-bmeng. " 기준 수량
      it_list-bmein = ls_topmat-bmein. " BOM의 기본 단위
      it_list-linecolor = 'C500'. " 레벨 1인 경우 그 row를 초록색으로 변경

* 1레벨이 아닌 경우 그 위의 모품목 수량을 가지고 온다.
    ELSEIF it_list-stufe NE 1.
      CLEAR it_stpox.
      READ TABLE it_stpox WITH KEY idnrk = it_list-matnr_m. " 하위 자재코드의 모품목과 일치하는 정보를 it_stpox에서 불러옴
      it_list-bmeng    = it_stpox-menge. " 모품목의 수량
      it_list-bmein    = it_stpox-meins. " 모품목의 단위

    ENDIF.

    APPEND it_list TO it_list2. " it_list 헤더라인에 담긴 정보를 it_list2 인터널 테이블에 추가.

  ENDLOOP.

ENDFORM.                    " BOM_EXPL
*&---------------------------------------------------------------------*
*&      Form  SEL_BOM_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  alv_obj
*&---------------------------------------------------------------------*
*       컨테이너, 그리드 오브젝트 생성
*----------------------------------------------------------------------*
FORM alv_obj .
  CREATE OBJECT con1_dock
    EXPORTING
      repid     = sy-repid " 컨트롤에 추가하고자 하는 프로그램 ID
      dynnr     = sy-dynnr " 컨트롤에 추가하고자 하는 스크린 번호
      SIDE         = con1_dock->DOCK_AT_BOTTOM
      extension = 2000.

* 화면 분할
  CREATE OBJECT gv_splitter
      EXPORTING
        PARENT  = con1_dock
        ROWS    = 2      " 몇개의 행으로 나눌것인지
        COLUMNS = 1.   " 몇개의 열로 나눌것인지

* top-of-page
  CALL METHOD gv_splitter->GET_CONTAINER
    EXPORTING
      ROW         = 1
      COLUMN    = 1
    RECEIVING
      CONTAINER = con1.

* ALV
  CALL METHOD gv_splitter->GET_CONTAINER
    EXPORTING
      ROW         = 2
      COLUMN    = 1
    RECEIVING
      CONTAINER = con2.

  CALL METHOD gv_splitter->SET_ROW_HEIGHT
    EXPORTING
      ID      = 1
      HEIGHT  = 15.

* 어떤 컨테이너 안에 들어갈 것인지 정의
*  CREATE OBJECT g_grid1
*    EXPORTING
*      i_parent = con1_dock.

*   CREATE OBJECT g_grid1
*      EXPORTING
*        i_parent = con1.

* ALV
    CREATE OBJECT g_grid2
      EXPORTING
        i_parent = con2.

  CREATE OBJECT g_dyndoc_id.
  CREATE OBJECT g_handler.

  SET HANDLER g_handler->top_of_page FOR g_grid2.

  CALL METHOD g_grid2->list_processing_events
    EXPORTING
      i_event_name = 'TOP_OF_PAGE'
      i_dyndoc_id = g_dyndoc_id.

ENDFORM.                    " ALV_OBJ
*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SETTING
*&---------------------------------------------------------------------*
*       ALV 레이아웃 요소 설정
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM alv_layout_setting.

  DATA : lv_title(20) TYPE c.
  CONCATENATE '플랜트' ' ' p_werks ' ' t001w-name1 INTO lv_title RESPECTING BLANKS. " respecting blanks를 해줘야 자동 trim 안생김
  gs_layout-zebra = 'X'.
  gs_layout-cwidth_opt = 'X'. " 칼럼 길이 자동 조절
  gs_layout-grid_title =  lv_title. " 플랜트 이름
  gs_layout-smalltitle =  'X'. " ALV 타이틀을 작은 글씨로
  gs_layout-sel_mode =  'A'. " sel_mode를 설정
  gs_layout-info_fname = 'LINECOLOR'.

ENDFORM.                    " ALV_LAYOUT_SETTING

*&---------------------------------------------------------------------*
*&      Form  SET_FIELDCAT
*&---------------------------------------------------------------------*
*       필드 카탈로그 레이아웃 설정
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_fieldcat .

  PERFORM append_field_catalog USING :
       'MATNR'    'X'      '상위 자재 코드'      ' '    ' '   ' '    'X'   ' '   ' ',
        'MAKTX'   ' '      '자재 내역'   ' '    ' '   ' '    ' '  ' '   ' ',
        'WERKS'   ' '      '플랜트 '   ' '    ' '     ' '   ' '   ' '   ' ',
        'STLAN'   ' '      '용도'  ' '    ' '   'C'    'X'  ' '   ' ',
        'STLAL'   ' '      '대체 BOM'  ' '    ' '    'C'    'X'  ' '   ' ',
        'STLST'   ' '      'BOM 상태'  ' '    ' '    'C'    ' '  ' '   ' ',
        'MTART'   ' '      '자재 그룹'  ' '    ' '     'R'  ' '  ' '   ' ',
        'STUFE'   ' '      '레벨'  ' '    ' '   'C'  ' '  ' '   ' ',
        'MATNR_SUB'   ' '    '구성부품'  ' '    ' '   'C'    'X'  ' '   ' ',
        'OJTXP'   ' '      '구성부품 내역'  ' '    ' '   'L'    ' '  ' '   ' ',
        'MENGE'   ' '      '수량'  ' '    ' '   'R'   ' '  '3'   ' ',
        'MEINS'   ' '      '단위'  ' '    ' '   'C'    ' '  '0'   ' ',
        'DATUV'   ' '      '효력시작일'  ' '    ' '   'C'    ' '  '0'   '____/__/__',
        'BMENG'   ' '      '기준수량'  ' '    ' '   'R'    ' '  '3'   ' ',
        'BMEIN'   ' '      '단위'  ' '    ' '   'L'    ' '  '0'   ' ',
        'AUSCH'   ' '      '구성부품스크랩'  ' '    ' '   'C'   ' '  '0'   ' ',
        'SANKA'   ' '      '원가계산관계도'  ' '    ' '   'L'   ' '  '0'   ' '.

ENDFORM.                    "SET_FIELDCAT

*&---------------------------------------------------------------------*
*&      Form  append_field_catalog
*&---------------------------------------------------------------------*
*       필드 카탈로그에 데이터 입력
*----------------------------------------------------------------------*
*      -->P_FNAME       text
*      -->P_KEY         text
*      -->P_OUTPUTLEN   text
*      -->P_COLTEXT     text
*      -->P_EDIT        text
*      -->P_EMPHASIZE   text
*      -->P_CFIELDNAME  text
*      -->P_JUST        text
*      -->P_ZERO        text
*      -->P_DECIMALS_O  text
*----------------------------------------------------------------------*
FORM append_field_catalog USING p_fname
                                p_key
                                p_coltext
                                p_edit
                                p_emphasize
                                p_just
                                p_zero
                                p_decimals_o
                                p_edit_mask.

  CLEAR gs_fieldcat.
    gs_fieldcat-fieldname = p_fname. " ALV에 나타낼 필드 이름
    gs_fieldcat-key = p_key." 고정 할 지?
    gs_fieldcat-coltext = p_coltext." 열 헤딩
    gs_fieldcat-edit = p_edit. " 수정 가능하게 할 지
    gs_fieldcat-emphasize = p_emphasize. " 열을 색상으로 강조 표시
    gs_fieldcat-just = p_just. " 정렬 L R C
    gs_fieldcat-no_zero = p_zero. " 옆에 0 제거
    gs_fieldcat-decimals_o = p_decimals_o. " 표시되는 소숫점 정의
    gs_fieldcat-edit_mask = p_edit_mask. " 날짜 표시 형식을 ____/__/__바꿈

  APPEND gs_fieldcat TO gt_fieldcat.

ENDFORM.                    "append_field_catalog
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV diplay에 필요한 설정 입력
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM alv_display .
  gs_variant-report = sy-repid. " 현재 프로그램
  gs_variant-username = sy-uname. " 현재 유저네임

* 데이터 출력 ALV 부분
  CALL METHOD g_grid2->set_table_for_first_display
    EXPORTING
      is_variant      = gs_variant " 레이아웃을 저장할 수 있게 함
      i_save          = 'A'
      i_default       = 'X'
      is_layout       = gs_layout
    CHANGING
      it_outtab       = it_list2[]
      it_fieldcatalog = gt_fieldcat.

  CALL METHOD g_dyndoc_id->initialize_document.
*    EXPORTING
*      background_color = cl_dd_area=>col_textarea.

  CALL METHOD g_grid2->list_processing_events
    EXPORTING
      i_event_name = 'TOP_OF_PAGE'
      i_dyndoc_id  = g_dyndoc_id.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  SET_TOP_PAGE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_G_DYNDOC_ID  text
*----------------------------------------------------------------------*
FORM SET_TOP_PAGE  USING  e_dyndoc_id  TYPE REF TO cl_dd_document.

  DATA: today TYPE SDYDO_TEXT_ELEMENT,
        plant TYPE SDYDO_TEXT_ELEMENT.

  today = sy-datum.
  plant = t001w-name1.

  CONCATENATE '조회일 : ' today+0(4) '년 ' today+4(2) '월 ' today+6(2) '일' INTO today RESPECTING BLANKS.

  e_dyndoc_id->add_text(
      text         = 'BOM 조회 레포트'
*      sap_style    = cl_dd_area=>key
      sap_fontsize = cl_dd_area=>LARGE
*      sap_color    = cl_dd_area=>list_heading_int
    ).

  e_dyndoc_id->new_line(
    ).
  e_dyndoc_id->new_line(
    ).

  CALL METHOD e_dyndoc_id->add_text
    exporting
     text         = today
     sap_color    = cl_dd_area=>list_group_int. "list_heading_int 민트색.
                                                "list_group_int 주황색.
                                                "list_negative_int 빨간색.
  e_dyndoc_id->new_line(
    ).

  CALL METHOD e_dyndoc_id->add_text
    exporting
     text         = '최예훈'.

  e_dyndoc_id->new_line(
    ).
  e_dyndoc_id->new_line(
    ).

    e_dyndoc_id->new_line(
    ).
   CALL METHOD e_dyndoc_id->add_text
    exporting
     text         = plant.

* 이거 안해주면 안보임
  CALL METHOD e_dyndoc_id->display_document
    EXPORTING
      reuse_control = 'X'
      parent        = con1.

ENDFORM.                    " SET_TOP_PAGE
```