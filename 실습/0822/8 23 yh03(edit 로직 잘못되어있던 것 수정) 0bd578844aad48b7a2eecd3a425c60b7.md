# 8/23 yh03(edit 로직 잘못되어있던 것 수정)

생성자: 최예훈
생성 일시: 2023년 8월 23일 오후 5:07
태그: 실습

## MAIN

```abap
*&---------------------------------------------------------------------*
*& Report  ZPPR_BOM_YH
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*
* ZPPR_BOM_YH02에 더블클릭 이벤트 설정
* 수정 화면으로 바뀔 때 수정가능한 칼럼 노란색으로 표시하는 부분 뺌. -> 이 부분은 필드 카탈로그emphasize 변경에
"     해당하는 부분이라 수정/조회 바뀔 때 바로 반영해서 보여주려면 set_table_for_first_display를 다시 해야해서 리소스 낭비되고 셀 위치가 초기화됨
* 그래서 refresh이용하여 linecolor 라인의 색 변경하는 부분만 남겨놓음.
* cellcolor 조작하는 방법으로 내가 생각한대로 만들 수 있을 것 같아서 그 부분 공부 예정
* 인터널 테이블에 담긴 데이터 변경시 ALV 새로고침 -> refresh_table_display
* ALV 레이아웃이 바뀌었을 경우 ->set_frontend_layout 해주고 refresh
* 필드 카탈로그가 바뀌었을 경우 ALV 새로고침 -> set_table_for_first_display 다시 써야함

REPORT  zppr_bom_yh03.

INCLUDE zppr_bom_yh03_top.
INCLUDE zppr_bom_yh03_cls. " 클래스 정의
INCLUDE zppr_bom_yh03_scr. " 1000번 스크린(조회 조건 화면)
INCLUDE zppr_bom_yh03_pbo. " 스크린 100번의 PBO
INCLUDE zppr_bom_yh03_pai. " 스크린 100번의 PAI
INCLUDE zppr_bom_yh03_frm. " 서브루틴 정리

*----------------------------------------------------------------------*
* INITIALIZATION                                                       *
*----------------------------------------------------------------------*
INITIALIZATION.
  PERFORM init_before_run. " 초기값을 넣어주어야 할 때 사용

*----------------------------------------------------------------------*
* START OF SELECTION                                                   *
*----------------------------------------------------------------------*
START-OF-SELECTION.

  PERFORM init_data. " 사용할 인터널 테이블, 구조체 초기화
  PERFORM select_data. " 조회값에 기반하여 BOM 정보 불러옴

*----------------------------------------------------------------------*
* END OF SELECTION                                                     *
*----------------------------------------------------------------------*
END-OF-SELECTION.
  CALL SCREEN 0100. " 조회 결과 ALV 를 담을 화면 100번 불러옴
```

## TOP

```abap
*&---------------------------------------------------------------------*
*&  Include           ZPPR_BOM_YH03_TOP
*&---------------------------------------------------------------------*

TABLES : marc, " 자재에 대한 플랜트 데이터
         mast, "  BOM 연결 자재
         mara, " 일반 자재 데이타
         stko, " BOM 헤더
         t001w. " 플랜트 상세 정보

TYPES : BEGIN OF result_type,

        matnr TYPE mara-matnr, " 상위자재코드
        maktx TYPE makt-maktx, " 자재내역
        werks TYPE marc-werks, " 플랜트
        stlan TYPE stpox-stlan, " 용도
        stlal TYPE mast-stlal, " 대체 BOM

        stlst TYPE stko-stlst, " BOM 상태
        mtart TYPE mara-mtart, " 자재 그룹
        stufe TYPE stpox-stufe, " 레벨
        matnr_m TYPE mara-matnr, " 구성부품의 상위 모품목 자재코드
        matnr_sub TYPE mara-matnr, " 구성부품의 자재코드
        ojtxp TYPE stpox-ojtxp, " 구성부품 내역

        menge TYPE stpox-menge, " 수량
        meins TYPE stpox-meins, " 단위
        datuv TYPE stpox-datuv, " 효력 시작일
        bmeng TYPE stko-bmeng, " 기준수량
        bmein TYPE stko-bmein, " 단위

        ausch TYPE stpox-ausch, " 구성부품스크랩
        sanka TYPE stpox-sanka, " 원가계산관계도

        stlnr TYPE stko-stlnr, " BOM

        linecolor(4) TYPE c, " 라인 색상 지정

        END OF result_type.

DATA : it_base TYPE TABLE OF result_type, " 초기화면 조건과 mara, marc, makt, mast 테이블을 이용해 BOM정보를 취득하여 저장할 인터널 테이블 (대상 필드는 화면 레이아웃의 테이블/필드 참조)
       it_base_str TYPE result_type,  " it_base는 헤더라인 없으므로 it_base_str 구조체 생성해주어야 함

       it_list TYPE TABLE OF result_type WITH HEADER LINE, " 제품 기준으로 하위품목을 가져와서 저장할 인터널 테이블
       it_list2 TYPE TABLE OF result_type WITH HEADER LINE. " 모든 자재에 대한 결과값을 담아 출력할 인터널 테이블

FIELD-SYMBOLS <it_base_fs> LIKE it_base_str. " 필드 심볼 선언
*-----------------------------------------------------------------
* 컨테이너, 그리드 관련 변수
DATA : con1_dock TYPE REF TO cl_gui_docking_container, " 도킹 컨테이너
       g_grid1 TYPE REF TO cl_gui_alv_grid, " 그리드
       g_grid2 TYPE REF TO cl_gui_alv_grid. " 그리드2

DATA : gv_splitter TYPE REF TO cl_gui_splitter_container,
       con1 TYPE REF TO cl_gui_container, " TOP-OF-PAGE 들어갈 컨테이너
       con2 TYPE REF TO cl_gui_container, " ALV 들어갈 컨테이너
       g_dyndoc_id TYPE REF TO cl_dd_document.

*-----------------------------------------------------------------
* ALV 레이아웃 관련 변수
DATA : gs_variant TYPE disvariant, " ALV 레이아웃 저장하기 위한 변수

       gs_layout TYPE lvc_s_layo, " ALV 레이아웃 지정
       gt_fieldcat TYPE lvc_t_fcat, " ALV 필드 카탈로그 인터널 테이블
       gs_fieldcat TYPE lvc_s_fcat. " ALV 필드 카탈로그 구조체

FIELD-SYMBOLS <gt_fieldcat_fs> LIKE gs_fieldcat. " 필드 심볼 선언

*------------------------------------------------------------------
* PAI 관련 변수
DATA : ok_code TYPE sy-ucomm. " 버튼 입력 받기 위한 변수

DATA : gv_edit_control TYPE INT4 VALUE 0. " 조회/수정모드 나타내는 변수

** EDIT 관련 변수
DATA : gt_modified_rows TYPE TABLE OF makt, " 수정한 row를 담을 변수
       gv_report_name TYPE sdydo_text_element VALUE 'BOM 조회',
       gv_edit_color(4) TYPE c VALUE 'C500'. " EDIT시 색 변경
```

## CLASS

```abap
*&---------------------------------------------------------------------*
*&  Include           ZPPR_BOM_YH03_CLS
*&---------------------------------------------------------------------*

*----------------------------------------------------------------------*
*       CLASS lcl_event DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_event DEFINITION. " TOP-OF-PAGE 생성 이벤트 반응하는 클래스
  PUBLIC SECTION.
    METHODS : top_of_page
      FOR EVENT top_of_page OF cl_gui_alv_grid " cl_gui_alv_grid에 정의된 TOP-OF-PAGE 이벤트를 감지
      IMPORTING e_dyndoc_id. " 이벤트 정의 시 IMPLEMENTATION구절에서 사용할 수 있는 e_dyndoc_id.라는 파라미터 생성함.(명시하지 않아도 상관 X지만 명시 하지 않으면 메소드에서 사용불가)
ENDCLASS.                    "lcl_event DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_event IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_event IMPLEMENTATION.
  METHOD top_of_page.
    PERFORM set_top_page USING e_dyndoc_id. " 정의하는 부분에서 IMPORTING했던 e_dyndoc_id.
  ENDMETHOD.                    "top_of_page
ENDCLASS.                    "lcl_event IMPLEMENTATION

DATA : g_handler TYPE REF TO lcl_event. " 정의한 이벤트 클래스로 인스턴스 생성.

*----------------------------------------------------------------------*
*       CLASS lcl_event_receiver DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_event_receiver DEFINITION. " 데이터 변경 이벤트 반응하는 클래스
  PUBLIC SECTION.
    METHODS: handle_data_changed
         FOR EVENT data_changed OF cl_gui_alv_grid " " cl_gui_alv_grid에 정의된 data_changed 이벤트를 감지
                  IMPORTING er_data_changed. " 이벤트 정의 시 IMPLEMENTATION구절에서 사용할 수 있는 er_data_changed라는 인스턴스 생성.(명시하지 않아도 상관 X지만 명시 하지 않으면 메소드에서 사용불가)
ENDCLASS.                    "lcl_event_receiver DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_event_receiver IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_event_receiver IMPLEMENTATION.
  METHOD handle_data_changed. " data_change 이벤트가 발생되면 아래 로직을 수행
    DATA : ls_makt TYPE makt, " 수정할 딕셔너리 테이블의 구조를 가진 구조체
           ls_outtab LIKE LINE OF it_list2,
           lt_outtab LIKE TABLE OF it_list2,
           lt_data_changed TYPE LINE OF lvc_t_modi.

    FIELD-SYMBOLS: <fs> TYPE table.

    " er_data_changed 바뀐 부분만 가져올 수 있음
    LOOP AT er_data_changed->mt_good_cells into lt_data_changed.
    ENDLOOP.

    " er_data_changed : ALV 그리드의 수정 내역을 저장하기 위해 이벤트가 생성하는 인스턴스
    " er_data_changed의 mp_mod_rows 메서드를 작동. 변화된 row가 담긴 인터널 테이블의 data refrence가 나온다.
    ASSIGN er_data_changed->mp_mod_rows->* TO <fs>. " 바뀐 데이터들을 필드 심볼에 역참조(필드 심볼 it_list2의 구조로 바뀐 데이터 row를 저장)

    LOOP AT <fs> INTO ls_outtab. " 바뀐 데이터들이 담긴 필드심볼에서 한 줄 씩 꺼냄
      " 모품목 자재 이름
      ls_makt-mandt = sy-mandt.
      ls_makt-matnr = ls_outtab-matnr.
      ls_makt-spras = sy-langu.
      ls_makt-maktx = ls_outtab-maktx.
      ls_makt-maktg = ls_outtab-maktx.
      APPEND ls_makt TO gt_modified_rows. " gt_modified_rows에 저장

      CLEAR ls_makt.
      " 구성부품 이름
      ls_makt-mandt = sy-mandt.
      ls_makt-matnr = ls_outtab-matnr_sub.
      ls_makt-spras = sy-langu.
      ls_makt-maktx = ls_outtab-ojtxp.
      ls_makt-maktg = ls_outtab-ojtxp.
      APPEND ls_makt TO gt_modified_rows. " gt_modified_rows에 저장

    ENDLOOP.
  ENDMETHOD.                    "handle_data_changed

ENDCLASS.                    "lcl_event IMPLEMENTATION

DATA : event_receiver TYPE REF TO lcl_event_receiver. " 정의한 이벤트 클래스로 인스턴스 생성.

*---------------------------------------------------------
* 더블 클릭 시 이벤트
CLASS lcl_click_event DEFINITION. "정의부
  PUBLIC SECTION.
    METHODS : handle_double_click
      FOR EVENT double_click OF cl_gui_alv_grid "  cl_gui_alv_grid 클래스에서 이벤트 따옴
      IMPORTING e_row e_column.

    METHODS : handle_hotspot_click
      FOR EVENT hotspot_click OF cl_gui_alv_grid "  cl_gui_alv_grid 클래스에서 이벤트 따옴
      IMPORTING e_row_id e_column_id.
ENDCLASS.                    "lcl_double_click_event DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_double_click_event IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_click_event IMPLEMENTATION.
  METHOD handle_double_click.
    IF gv_edit_control = 0.
      gv_edit_control = 1.

    ELSEIF gv_edit_control = 1.
      gv_edit_control = 0.

    ENDIF.

      PERFORM col_line.
      PERFORM refresh_alv.
      PERFORM display_top_of_page.

  ENDMETHOD.                    "handle_double_click

  METHOD handle_hotspot_click.

  ENDMETHOD.                    "handle_hotspot_click
ENDCLASS.                    "lcl_event IMPLEMENTATION

DATA : double_click_e_receiver TYPE REF TO lcl_click_event .
DATA : hotspot_click_e_receiver TYPE REF TO lcl_click_event .
```

## SCR

```abap
*&---------------------------------------------------------------------*
*&  Include           ZPPR_BOM_YH03_SCR
*&---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK sel_01 WITH FRAME TITLE text-001.

PARAMETERS : p_datuv LIKE stko-datuv DEFAULT sy-datum, " 효력 시작일
             p_werks LIKE marc-werks OBLIGATORY DEFAULT 'M960'. " 플랜트

SELECT-OPTIONS :  s_matnr FOR mara-matnr VISIBLE LENGTH 10, " 자재코드
                  s_stlan FOR mast-stlan VISIBLE LENGTH 10, " 용도
                  s_stlal FOR mast-stlal. " 대체 BOM "

PARAMETERS :  p_mtart LIKE mara-mtart DEFAULT 'ZFRT'. " 자재 유형

SELECTION-SCREEN END OF BLOCK sel_01.

SELECTION-SCREEN BEGIN OF BLOCK sel_02 WITH FRAME TITLE text-002.

  PARAMETERS : p_vari like disvariant-variant.

SELECTION-SCREEN END OF BLOCK sel_02.

* 레이아웃 선택
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  gs_variant-report = sy-repid.
  call FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      is_variant = gs_variant
      i_save = 'A'
    IMPORTING
      es_variant = gs_variant
    EXCEPTIONS
      not_found = 1
      program_error = 2
      OTHERS = 3.

IF sy-subrc = 0.
  p_vari = gs_variant-variant.
ENDIF.
```

## PBO

```abap
*&---------------------------------------------------------------------*
*&  Include           ZPPR_BOM_YH03_PBO
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Module  STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*

MODULE STATUS_0100 OUTPUT.
  SET PF-STATUS 'B100'.
  SET TITLEBAR 'YHTITLE'.
ENDMODULE.                 " STATUS_0100  OUTPUT

MODULE set_alv OUTPUT.

  IF con1_dock is INITIAL. " 처음 ALV가 생성되었을 때.

    PERFORM alv_obj. " 컨테이너와 그리드 생성
    PERFORM alv_layout_setting. " ALV 레이아웃 설정
    PERFORM set_fieldcat. " 필드 카탈로그 설정
    PERFORM alv_display. " ALV 출력

  ELSE. " 조회/수정 모드 버튼을 누른 경우

    PERFORM col_line." 라인의 색 변경
    PERFORM refresh_alv. " ALV 새로고침
*       PERFORM alv_display. " 이걸 써도 비슷하게 화면 갱신 가능하지만 커서 위치가 초기화됨
    PERFORM display_top_of_page. " TOP-OF-PAGE 새로고침

  ENDIF.

ENDMODULE.                 " SET_ALV  OUTPUT
```

## PAI

```abap
*----------------------------------------------------------------------*
***INCLUDE ZPPR_BOM_YH03_PAI .
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_0100  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE user_command_0100 INPUT.
  DATA : lv_answer TYPE c.

  CASE ok_code.
    WHEN 'BACK' OR 'CANC'.
      LEAVE TO SCREEN 0. " 이전 화면으로 가세옹
    WHEN 'EXIT'.
      CALL FUNCTION 'POPUP_TO_CONFIRM_STEP' " 데아터의 손실 가능성이 있는 작업을 수행할 때 사용되는 함수
        EXPORTING
          textline1 = '프로그램을 종료하시겠습니까?' " 팝업창의 본문 내용
          start_column        = 30 " 팝업창 위치
          start_row           = 6
          defaultoption       = 'Y' " 디폴트 설정 값을 Y 값으로 한다.
          titel     = '프로그램 종료' " 팝업창의 제목
        IMPORTING
          answer    = lv_answer. "팝업창에 입력한 값을 저장

      CHECK lv_answer NE 'N'. " N을 입력하지 않을 시 = Y를 입력했을 시
      LEAVE PROGRAM. " 프로그램 종료

    WHEN 'SAVE'. " 저장 버튼 눌렀을 때
      DATA : l_valid TYPE c. " 데이터 변화가 있는지 없는지 여부를 저장할 변수. 데이터 변화가 있으면 'X'
      CALL METHOD g_grid2->check_changed_data " check_changed_data메서드 실행
        IMPORTING
            e_valid = l_valid. " e_valid : 데이터 변화가 있는지?
      IF l_valid IS NOT INITIAL. " 데이터 변화가 있으면 = l_valid가 'X'면
        PERFORM update_database. " 데이터베이스에 바뀐 데이터 저장하는 서브루틴 실행

      ENDIF.

    WHEN 'EDIT'.
      IF gv_edit_control = 0. " 조회 모드일때 EDIT을 누르면
        gv_edit_control = 1. " 수정 모드로 변경

      ELSEIF gv_edit_control = 1. " 수정 모드일때 EDIT을 누르면
        gv_edit_control = 0. " 조회 모드로 변경

      ENDIF.
    WHEN OTHERS.
      MESSAGE 'ERROR' TYPE 'E'.
  ENDCASE.

ENDMODULE.                 " USER_COMMAND_0100  INPUT

*&---------------------------------------------------------------------*
*&      Form  UPDATE_DATABASE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM update_database . " 실제로 데이터베이스에 저장하는 서브루틴
  DATA : ls_stable TYPE lvc_s_stbl.

  ls_stable-row = 'X'.
  ls_stable-col = 'X'.

  " gt_modified_rows는 이벤트 핸들러가 data_change를 감지하여 만들어놓음
  MODIFY makt FROM TABLE gt_modified_rows. " 변경한 정보가 담긴 gt_modified_rows를 원래 데이터베이스 테이블에 저장
  IF sy-subrc = 0.
    MESSAGE '저장되었습니다.' TYPE 'I'.

    CLEAR : it_list2, it_list2[]. " 결과값을 보여줄 인터널 테이블을 초기화한다.
    PERFORM select_data. " 데이터가 변경되었으므로 다시 데이터를 불러온다.

    CALL METHOD g_grid2->refresh_table_display " ALV를 새로고침한다.
      EXPORTING
        is_stable = ls_stable.

  ENDIF.
ENDFORM.                    " UPDATE_DATABASE
```

## SUBROUTINE

```abap
*&---------------------------------------------------------------------*
*&  Include           ZPPR_BOM_YH03_FRM
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM select_data.

  PERFORM get_werks_name. " 작업장 이름 불러옴
  PERFORM sel_bom_data. " 조건값에 맞는 BOM 정보 조회
  PERFORM loading. " BOM 정보를 조회중입니다 표시
  PERFORM msg. " 조회중 메시지 출력

  LOOP AT it_base ASSIGNING <it_base_fs>. " INTO it_base_str. " 입력받은 자재번호 range를 순회하면서 조회
    PERFORM bom_expl. " 하위 BOM 조회
  ENDLOOP.

ENDFORM.                    " SELECT_DATA
*&---------------------------------------------------------------------*
*&      Form  INIT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM init_data .
  CLEAR :  con1_dock, g_grid1,
           gs_variant, gs_layout,
           gs_fieldcat, gt_fieldcat,
           it_list, it_list[],
           it_list2, it_list2[],
           it_base, it_base_str.

ENDFORM.                    " INIT_DATA

*&---------------------------------------------------------------------*
*&      Form  get_werks_name
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_werks_name .
  SELECT name1 INTO CORRESPONDING FIELDS OF t001w
    FROM t001w
    WHERE werks = p_werks.
  ENDSELECT.
ENDFORM.                    " GET_WERKS_NAME
* 사용할 인터널 테이블, 구조체 초기값 넣어줌
FORM init_before_run.

ENDFORM.                    "INIT_BEFORE_RUN

*&---------------------------------------------------------------------*
*&      Form  loading
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM loading.

  DATA : lv_text(50) TYPE c.

  lv_text = 'BOM 정보를 조회중입니다... '.

  CALL FUNCTION
    'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      text   = lv_text
    EXCEPTIONS
      OTHERS = 0.

ENDFORM.                    "loading

*&---------------------------------------------------------------------*
*&      Form  sel_bom_data
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM sel_bom_data .
  SELECT b~matnr c~maktx a~werks a~stlan a~stlal a~stlnr INTO CORRESPONDING FIELDS OF TABLE it_base
    FROM mast AS a INNER JOIN mara AS b
                       ON a~matnr = b~matnr
                 INNER JOIN makt AS c
                       ON a~matnr = c~matnr
    WHERE a~werks = p_werks
        AND a~matnr IN s_matnr " 입력받은 자재코드
        AND a~stlan IN s_stlan " 입력받은 용도
        AND a~stlal IN s_stlal " 입력받은 대체 BOM
        AND b~mtart = p_mtart  " 입력받은 자재 유형
        AND c~spras = sy-langu. " 현재 시스템 언어

  DELETE ADJACENT DUPLICATES FROM it_base COMPARING stlnr. "  stlnr(BOM)기준으로 중복 제거
  SORT it_base BY matnr. " matnr 기준으로 정렬

ENDFORM.                    " SEL_BOM_DATA
*&---------------------------------------------------------------------*
*&      Form  MSG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM msg .
  IF it_base IS NOT INITIAL.. " 인터널 테이블이 비어있지 않을 경우 = 조회값이 있을경우
    MESSAGE '정상 조회되었습니다.' TYPE 'S'.

  ELSE.
    MESSAGE '조회할 데이터가 없습니다.' TYPE 'I'. " 조회값이 없습니다 팝업 출력
    LEAVE LIST-PROCESSING. " 리스트 처리를 중단하고 빠져나옴(조회 페이지로 다시 감)
  ENDIF.
ENDFORM.                    " MSG
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  BOM_EXPL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM bom_expl.
  DATA : it_stpox TYPE TABLE OF stpox WITH HEADER LINE WITH NON-UNIQUE KEY idnrk, " BOM EXPL 결과 저장할 인터널 테이블
         ls_topmat TYPE cstmat, " 최상위 자재 정보 저장할 구조체
         it_cscmat TYPE TABLE OF cscmat WITH HEADER LINE. " BOM 카테고리를 저장할 인터널 테이블

  CLEAR : it_list, it_list[], it_stpox, it_stpox[], ls_topmat, it_cscmat, it_cscmat[]. " 서브루틴에서 사용할 테이블들, 구조체들 초기화.

  CALL FUNCTION 'CS_BOM_EXPL_MAT_V2'
    EXPORTING
      capid                 = 'PP01' " 어플리케이션
      datuv                 = p_datuv " 효력시작일
      emeng                 = 0
      mehrs                 = 'X' " 다중 BOM 지시자 - 없으면 단일 레벨만 읽는다
      mtnrv                 = <it_base_fs>-matnr " 상위 자재 번호(모자재)
      werks                 = p_werks " 플랜트
      stlan                 = <it_base_fs>-stlan " BOM 용도
      stlal                 = <it_base_fs>-stlal " 대체 BOM
    IMPORTING
      topmat                = ls_topmat " 최상위 자재 번호를 저장
    TABLES
      stb                   = it_stpox[] " BOM EXPL 결과 저장
      matcat                = it_cscmat[] " 자재 카테고리
    EXCEPTIONS
      alt_not_found         = 1
      call_invalid          = 2
      missing_authorization = 4
      no_bom_found          = 5
      no_plant_data         = 6
      no_suitable_bom_found = 7
      conversion_error      = 8
      OTHERS                = 9.

* 4에서 취득한 it_stpox 기준으로 for all entries in 사용 하여, 제품 기준으로 하위 품목을 가지고 온다.
* it_list = 가져온 하위 품목이 담긴 리스트
  SELECT ma~matnr kt~maktx
      INTO CORRESPONDING FIELDS OF TABLE it_list
      FROM mara AS ma INNER JOIN marc AS mc
                             ON mc~matnr = ma~matnr
                      INNER JOIN makt AS kt
                             ON ma~matnr = kt~matnr
    FOR ALL ENTRIES IN it_stpox
    WHERE ma~matnr = it_stpox-idnrk
    AND kt~spras = sy-langu.

  LOOP AT it_stpox. " BOM EXPL에서 얻은 하위 자재 정보 순회하면서
    READ TABLE it_list WITH KEY matnr = it_stpox-idnrk. " 자재코드를 KEY로 하여 구성 부품 정보 가져와서 it_list의 정보와 매칭하여 it_list의 헤더 라인에 정보를 담는다.

    " 이후 이 it_list 헤더라인을 원하는 정보를 담아서 수정
    it_list-werks = <it_base_fs>-werks. " 최상위 모품목 작업장
    it_list-matnr = <it_base_fs>-matnr. " 최상위 모품목 자재코드
    it_list-ojtxp = it_list-maktx.    " 구성부품 이름
    it_list-maktx = <it_base_fs>-maktx. " 최상위 모품목 이름
    it_list-stlan = <it_base_fs>-stlan. " 최상위 모품목 용도
    it_list-stlal = <it_base_fs>-stlal. " 최상위 모품목 대체 BOM
    it_list-stlst = ls_topmat-stlst. " 조회 제품의 대체 BOM
    it_list-stufe = it_stpox-stufe.    " 구성부품 레벨
*    it_list-ojtxp = it_stpox-ojtxp.    " 구성부품 이름(BOM 수정하고 저장하면 바로 반영 안되는 문제로 인해 바꿈 - 왜 bom expl이 갱신이 안될까?)

    READ TABLE it_cscmat WITH KEY INDEX = it_stpox-ttidx. " 자재 카테고리(it_cscmat)에서 BOM 단계(INDEX: 범주 테이블 내 인덱스)를 키로 하여 모품목의 자재코드를 가져옴
    it_list-matnr_m = it_cscmat-matnr. " 구성부품의 그 위의 모품목 자재코드
    it_list-matnr_sub = it_stpox-idnrk. " 구성부품의 자재코드
    it_list-menge = it_stpox-mngko.    " 구성부품수량
    it_list-meins = it_stpox-meins.    " 구성부품 단위
    it_list-mtart = it_stpox-mtart.    " 구성부품 용도
    it_list-datuv = it_stpox-datuv.    " 구성부품 효력 시작일
    it_list-stlal = it_stpox-stlal.    " 구성부품 대체 BOM
    it_list-ausch = it_stpox-ausch.    " 구성 부품 스크랩
    it_list-sanka = it_stpox-sanka.    " 원가 계산 관련 지시자

* 1레벨의 경우에만 기준수량을 가져온다."최상위 인경우 bom 헤더 수량을 가지고 온다
    IF it_list-stufe EQ 1.
      it_list-bmeng = ls_topmat-bmeng. " 기준 수량
      it_list-bmein = ls_topmat-bmein. " BOM의 기본 단위

* 1레벨이 아닌 경우 그 위의 모품목 수량을 가지고 온다.
    ELSEIF it_list-stufe NE 1.
      CLEAR it_stpox.
      READ TABLE it_stpox WITH KEY idnrk = it_list-matnr_m. " 하위 자재코드의 모품목과 일치하는 정보를 it_stpox에서 불러옴
      it_list-bmeng    = it_stpox-menge. " 모품목의 수량
      it_list-bmein    = it_stpox-meins. " 모품목의 단위

    ENDIF.

    APPEND it_list TO it_list2. " it_list 헤더라인에 담긴 정보를 it_list2 인터널 테이블에 추가.

  ENDLOOP.

ENDFORM.                    " BOM_EXPL
*&---------------------------------------------------------------------*
*&      Form  SEL_BOM_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  alv_obj
*&---------------------------------------------------------------------*
*       컨테이너, 그리드 오브젝트 생성
*----------------------------------------------------------------------*
FORM alv_obj .
  CREATE OBJECT con1_dock
    EXPORTING
      repid     = sy-repid " 컨트롤에 추가하고자 하는 프로그램 ID
      dynnr     = sy-dynnr " 컨트롤에 추가하고자 하는 스크린 번호
      side      = con1_dock->dock_at_bottom
      extension = 2000.

* 컨테이너 분할
* 1.도킹 컨테이너를 분할
* 2.분할한 row에 컨테이너 할당
  CREATE OBJECT gv_splitter
    EXPORTING
      parent  = con1_dock " 도킹 컨테이너를 분할
      rows    = 2      " 몇개의 행으로 나눌것인지
      columns = 1.   " 몇개의 열로 나눌것인지

" TOP-OF-PAGE가 들어갈 컨테이너 con1
  CALL METHOD gv_splitter->get_container
    EXPORTING
      row       = 1 " gv_splitter로 나눈 컨테이너 row중 1번 row에 con1 할당
      column    = 1
    RECEIVING
      container = con1.

* ALV가 들어갈 컨테이너 con2
  CALL METHOD gv_splitter->get_container
    EXPORTING
      row       = 2 " gv_splitter로 나눈 컨테이너 row중 2번 row에 con2 할당
      column    = 1
    RECEIVING
      container = con2.

* 전체 컨테이너 높이 할당.
  CALL METHOD gv_splitter->set_row_height
    EXPORTING
      id     = 1
      height = 10.

* ALV
  CREATE OBJECT g_grid2 " ALV grid가 들어갈 컨테이너 지정.
    EXPORTING
      i_parent = con2.

*----------------------------------------------
* grid에 핸들러를 설정
  CREATE OBJECT g_dyndoc_id.

  CREATE OBJECT g_handler. " top-of-page 핸들러
  SET HANDLER g_handler->top_of_page FOR g_grid2. "top_of_page 이벤트를 감지하는 핸들러 g_grid2에 설정

  CREATE OBJECT event_receiver. " 데이터 수정 이벤트 핸들러
  SET HANDLER event_receiver->handle_data_changed FOR g_grid2. " data_changed 이벤트를 감지하는 핸들러 g_grid2에 설정

  CREATE OBJECT double_click_e_receiver. " 더블클릭 이벤트 핸들러
  SET HANDLER double_click_e_receiver->handle_double_click FOR g_grid2. " double click 이벤트 감지하는 핸들러 g_grid2에 설정

*  CREATE OBJECT hotspot_click_e_receiver. " hotspot 이벤트 핸들러
*  SET HANDLER hotspot_click_e_receiver->handle_hotspot_click FOR g_grid2. " hotspot click 이벤트 감지하는 핸들러 g_grid2에 설정

ENDFORM.                    " ALV_OBJ
*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SETTING
*&---------------------------------------------------------------------*
*       ALV 레이아웃 요소 설정
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM alv_layout_setting.

  DATA : lv_title(20) TYPE c.
  CONCATENATE '플랜트' ' ' p_werks ' ' t001w-name1 INTO lv_title RESPECTING BLANKS. " respecting blanks를 해줘야 자동 trim 안생김
  gs_layout-zebra = 'X'.
*  gs_layout-cwidth_opt = 'X'. " 칼럼 길이 자동 조절
  gs_layout-grid_title =  lv_title. " 플랜트 이름
  gs_layout-smalltitle =  'X'. " ALV 타이틀을 작은 글씨로
  gs_layout-sel_mode =  'A'. " sel_mode를 설정
  gs_layout-info_fname = 'LINECOLOR'.
*  gs_layout-edit = 'X'. " ALV 전체 수정 가능하게 변경

ENDFORM.                    " ALV_LAYOUT_SETTING

*&---------------------------------------------------------------------*
*&      Form  SET_FIELDCAT
*&---------------------------------------------------------------------*
*       필드 카탈로그 레이아웃 설정
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_fieldcat .

  PERFORM append_field_catalog USING :
       'MATNR'    'X'  '상위 자재 코드' ' '    ' '     ' '    ' '    'X'   ' '   ' '  ' ',
        'MAKTX'   ' '  '자재 내역'     '40'   'X'     ' '    'X'    ' '  ' '   ' '  ' ',
        'WERKS'   ' '  '플랜트'       ' '     ' '    ' '     ' '   ' '   ' '   ' '  ' ',
        'STLAN'   ' '  '용도'         ' '     ' '    ' '    'C'    'X'  ' '   ' '  ' ',
        'STLAL'   ' '  '대체 BOM'     ' '     ' '    ' '    'C'    'X'  ' '   ' '  ' ',
        'STLST'   ' '  'BOM 상태'     ' '     ' '    ' '    'C'    ' '  ' '   ' '  ' ',
        'MTART'   ' '  '자재 그룹'     ' '     ' '    ' '    'R'    ' '  ' '   ' '  ' ',
        'STUFE'   ' '  '레벨'  ' '    ' '     ' '    'C'    ' '    ' '   ' '  ' ',
        'MATNR_SUB'   ' '  '구성부품'  ' '     ' '    ' '    'C'    'X'  ' '   ' '  ' ',
        'OJTXP'   ' '  '구성부품 내역'  '40'    'X'    ' '    'L'    ' '  ' '   ' '  ' ',
        'MENGE'   ' '  '수량'  '30'   ' '     ' '   'R'    ' '    '3'   ' '  ' ',
        'MEINS'   ' '  '단위'  ' '    ' '     ' '    'C'    ' '    '0'   ' '  ' ',
        'DATUV'   ' '  '효력시작일'     '15'    ' '    ' '    'C'    ' '  '0'   '____/__/__'  ' ',
        'BMENG'   ' '  '기준수량'      '25'     ' '   ' '    'R'    ' '  '3'   ' '  ' ',
        'BMEIN'   ' '  '단위'         ' '     ' '    ' '    'L'    ' '  '0'   ' '  ' ',
        'AUSCH'   ' '  '구성부품스크랩'  ' '     ' '    ' '    'C'    ' '  '0'   ' '  ' ',
        'SANKA'   ' '  '원가계산관계도'  ' '     ' '    ' '    'L'    ' '  '0'   ' '  ' '.

ENDFORM.                    "SET_FIELDCAT

*&---------------------------------------------------------------------*
*&      Form  append_field_catalog
*&---------------------------------------------------------------------*
*       필드 카탈로그에 데이터 입력
*----------------------------------------------------------------------*
*      -->P_FNAME       text
*      -->P_KEY         text
*      -->P_OUTPUTLEN   text
*      -->P_EDIT        text
*      -->P_EMPHASIZE   text
*      -->P_CFIELDNAME  text
*      -->P_JUST        text
*      -->P_ZERO        text
*      -->P_DECIMALS_O  text
*----------------------------------------------------------------------*
FORM append_field_catalog USING p_fname
                                p_key
                                p_coltext
                                p_outputlen
                                p_edit
                                p_emphasize
                                p_just
                                p_zero
                                p_decimals_o
                                p_edit_mask
                                p_hotspot.

  CLEAR gs_fieldcat.
  gs_fieldcat-fieldname = p_fname. " ALV에 나타낼 필드 이름
  gs_fieldcat-key = p_key." 고정 할 지?
  gs_fieldcat-coltext = p_coltext." 열 헤딩
  gs_fieldcat-outputlen = p_outputlen." 열 길이
  gs_fieldcat-edit = p_edit. " 수정 가능하게 할 지
  gs_fieldcat-emphasize = p_emphasize. " 열을 색상으로 강조 표시
  gs_fieldcat-just = p_just. " 정렬 L R C
  gs_fieldcat-no_zero = p_zero. " 옆에 0 제거
  gs_fieldcat-decimals_o = p_decimals_o. " 표시되는 소숫점 정의
  gs_fieldcat-edit_mask = p_edit_mask. " 날짜 표시 형식을 ____/__/__바꿈
  gs_fieldcat-hotspot = p_hotspot. " 칼럼에 hotspot 설정

  APPEND gs_fieldcat TO gt_fieldcat.

ENDFORM.                    "append_field_catalog
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV diplay에 필요한 설정 입력
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM alv_display .
  gs_variant-report = sy-repid. " 현재 프로그램
  gs_variant-username = sy-uname. " 현재 유저네임

  PERFORM col_line." 출력하는 ALV row의 색 지정

* 처음에는 조회모드
  CALL METHOD g_grid2->set_ready_for_input
    EXPORTING
      i_ready_for_input = gv_edit_control." 1은 입력 가능 상태, 0은 입력 불가능 상태

* 데이터 출력 ALV 부분
  CALL METHOD g_grid2->set_table_for_first_display
    EXPORTING
      is_variant      = gs_variant " 레이아웃을 저장할 수 있게 함
      i_save          = 'A'
      i_default       = 'X'
      is_layout       = gs_layout
    CHANGING
      it_outtab       = it_list2[]
      it_fieldcatalog = gt_fieldcat.

  PERFORM display_top_of_page.

* 엔터 치면 변경된 값이 인터널테이블에 저장됨
  CALL METHOD g_grid2->register_edit_event
    EXPORTING
      i_event_id = cl_gui_alv_grid=>mc_evt_enter.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  SET_TOP_PAGE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_G_DYNDOC_ID  text
*----------------------------------------------------------------------*
FORM set_top_page  USING  e_dyndoc_id  TYPE REF TO cl_dd_document.
  " TOP-OF-PAGE에 나타낼 데이터
  DATA: lv_today TYPE sdydo_text_element,
        lv_nowtime TYPE sdydo_text_element,
        lv_plant TYPE sdydo_text_element.

  lv_today = sy-datum. " 조회 일자(현재 날짜)
  lv_nowtime = sy-uzeit. " 조회 시간(현재 시간)
  lv_plant = t001w-name1. " 작업장명

  CONCATENATE '현재 시간 : ' lv_today+0(4) '년 ' lv_today+4(2) '월 ' lv_today+6(2) '일' '   ' " 현재 날짜
               lv_nowtime+0(2) ':' lv_nowtime+2(2) ':' lv_nowtime+4(2) " 현재 시간
               INTO lv_today RESPECTING BLANKS.

*  e_dyndoc_id->add_text(
*      text         = lv_name
**      sap_style    = cl_dd_area=>key
**      sap_fontsize = cl_dd_area=>large
**      sap_color    = cl_dd_area=>list_heading_int
*    ).

  CALL METHOD e_dyndoc_id->add_text
    EXPORTING
      text      = lv_today
      sap_color = cl_dd_area=>list_group_int. "list_heading_int 민트색.
  "list_group_int 주황색.
  "list_negative_int 빨간색.
  e_dyndoc_id->new_line( ).
  e_dyndoc_id->new_line( ).
  CALL METHOD e_dyndoc_id->add_text
    EXPORTING
      text = '최예훈'.

  e_dyndoc_id->new_line( ).
  e_dyndoc_id->new_line( ).

  DATA : lo_table TYPE REF TO cl_dd_table_element,
         lo_key TYPE REF TO cl_dd_area,
         lo_info TYPE REF TO cl_dd_area,
         lv_text TYPE sdydo_text_element.

  CALL METHOD e_dyndoc_id->add_table
    EXPORTING
      no_of_columns = 2
      width         = '500'
      border        = '1'
    IMPORTING
      table         = lo_table.

  CALL METHOD lo_table->add_column
    IMPORTING
      column = lo_key.

  CALL METHOD lo_table->add_column
    IMPORTING
      column = lo_info.

  lv_text = gv_report_name.
  lo_key->add_text(
                    text = lv_text
                    sap_emphasis = cl_dd_area=>strong
                    sap_fontsize = cl_dd_document=>large
  ).
  lo_key->add_gap(
                    width = '1'
  ).

  lo_info->add_text(
                    text = lv_plant
  ).

  lo_info->add_gap(
                    width = '3'
  ).

  e_dyndoc_id->merge_document( ).

* 이거 안해주면 안보임
  CALL METHOD e_dyndoc_id->display_document
    EXPORTING
      reuse_control      = 'X'
      reuse_registration = 'X'
      parent             = con1.

ENDFORM.                    " SET_TOP_PAGE
*&---------------------------------------------------------------------*
*&      Form  SET_TOP_OF_PAGE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM display_top_of_page .
  IF gv_edit_control = 0.
    gv_report_name = 'BOM 조회'.
  ELSEIF gv_edit_control = 1.
    gv_report_name = 'BOM 수정'.
  ENDIF.

*  TOP-OF-PAGE 초기화
  CALL METHOD g_dyndoc_id->initialize_document.
*    EXPORTING
*      NO_MARGINS = 'X' " TOP-OF-PAGE에 들어갈 text 마진 없앰
*      background_color = cl_dd_area=>col_textarea.
*      FIRST_TIME = 'X'. " 처음 시작 시에만 초기화

* TOP-OF-PAGE
  CALL METHOD g_grid2->list_processing_events
    EXPORTING
      i_event_name = 'TOP_OF_PAGE'
      i_dyndoc_id  = g_dyndoc_id.

ENDFORM.                    " SET_TOP_OF_PAGE
*&---------------------------------------------------------------------*
*&      Form  REFRESH_ALV
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM REFRESH_ALV .
    DATA : ls_stable TYPE lvc_s_stbl.

    ls_stable-row = 'X'.
    ls_stable-col = 'X'.
     CALL METHOD g_grid2->set_ready_for_input
       EXPORTING
         i_ready_for_input = gv_edit_control." 1은 입력 가능 상태, 0은 입력 불가능 상태

     CALL METHOD g_grid2->refresh_table_display
       EXPORTING
         is_stable = ls_stable.
ENDFORM.                    " REFRESH_ALV
*&---------------------------------------------------------------------*
*&      Form  COL_LINE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM COL_LINE .
  FIELD-SYMBOLS : <it_list2_fs> like LINE OF it_list2.
  CASE gv_edit_control.
      WHEN 1. " 수정 모드일 때

        " modify는 퍼포먼스를 떨어뜨리기 때문에 필드 심볼이 유용
        LOOP AT it_list2 ASSIGNING <it_list2_fs>.
           <it_list2_fs>-linecolor = ''.
        ENDLOOP.
*         MODIFY it_list2 transporting linecolor WHERE stufe EQ 1 OR stufe NE 1.

         LOOP AT gt_fieldcat ASSIGNING <gt_fieldcat_fs> WHERE fieldname = 'MAKTX' OR fieldname = 'OJTXP'.
           <gt_fieldcat_fs>-emphasize = 'C300'.
         ENDLOOP.

     WHEN 0. " 조회 모드일 때

        LOOP AT it_list2 ASSIGNING <it_list2_fs> WHERE stufe EQ 1.
           <it_list2_fs>-linecolor = 'C500'.
        ENDLOOP.
        LOOP AT it_list2 ASSIGNING <it_list2_fs> WHERE stufe NE 1.
           <it_list2_fs>-linecolor = ''.
        ENDLOOP.

*        it_list2-linecolor = 'C500'.
*        MODIFY it_list2 transporting linecolor WHERE stufe EQ 1.
*        it_list2-linecolor = ''.
*        MODIFY it_list2 transporting linecolor WHERE stufe NE 1.

        LOOP AT gt_fieldcat ASSIGNING <gt_fieldcat_fs> WHERE fieldname = 'MAKTX' OR fieldname = 'OJTXP'.
           <gt_fieldcat_fs>-emphasize = ''.
        ENDLOOP.

    WHEN OTHERS.
  ENDCASE.
ENDFORM.                    " COL_LINE
```