# 자재 조회 프로그램(2번째 레포트)

생성자: 최예훈
생성 일시: 2023년 8월 1일 오후 5:19
태그: 실습

```abap
*&---------------------------------------------------------------------*
*&  Include           ZRRP_YH006TOP
*&---------------------------------------------------------------------*

TABLES: mara, marc, mbew, makt. " 사용할 테이블 불러오기

* mara와 makt를 join한 테이블을 저장할 인터널 테이블
TYPES: BEGIN OF result_tab,
         matnr TYPE mara-matnr, " 자재 번호
         ernam TYPE mara-ernam, " 오브젝트 생성자 이름
         ersda TYPE mara-ersda, " 생성일

         werks TYPE marc-werks, " 플랜트
         pstat TYPE marc-pstat, " 유지보수 상태
         dismm TYPE marc-dismm, " MRP 유형

         lbkum TYPE mbew-lbkum, " 총 평가 재고
         salk3 TYPE mbew-salk3, " 평가 재고 총액
         stprs TYPE mbew-stprs, " 표준가

         maktx TYPE makt-maktx, " 자재명
       END OF result_tab.

DATA: lt_itab  TYPE TABLE OF result_tab WITH HEADER LINE. " lt_result는 result_tab 형식의 내부 테이블
```

```abap
*&---------------------------------------------------------------------*
*&  Include           ZRRP_YH006SRC
*&---------------------------------------------------------------------*

* 조회 화면
SELECTION-SCREEN BEGIN OF BLOCK sel_01 WITH FRAME TITLE text-001.

PARAMETERS : s_werks LIKE marc-werks, " 플랜트
             s_dismm LIKE marc-dismm OBLIGATORY. " MRP 유형

SELECT-OPTIONS : s_ersda FOR mara-ersda, " 생성일
                 s_stprs FOR mbew-stprs. " 표준가

SELECTION-SCREEN END OF BLOCK sel_01.

****************************************************
* FORM
****************************************************

* 조회 조건에 맞는 데이터 조회
FORM select_data.
  IF s_werks IS NOT INITIAL." 플랜트 입력했을 시
    SELECT m~matnr m~ernam m~ersda c~werks c~pstat c~dismm w~lbkum w~salk3 w~stprs t~maktx INTO CORRESPONDING FIELDS OF TABLE lt_itab
    FROM mara AS m
    INNER JOIN mbew AS w
    ON m~matnr = w~matnr
    INNER JOIN marc AS c
    ON w~matnr = c~matnr
    INNER JOIN makt AS t
    ON c~matnr = t~matnr
    WHERE werks = s_werks
         AND dismm = s_dismm
         AND ersda IN s_ersda
         AND stprs IN s_stprs.

  ELSE. " 플랜트 입력 안했을 시
    SELECT m~matnr m~ernam m~ersda c~werks c~pstat c~dismm w~lbkum w~salk3 w~stprs t~maktx INTO CORRESPONDING FIELDS OF TABLE lt_itab
    FROM mara AS m
    INNER JOIN mbew AS w
    ON m~matnr = w~matnr
    INNER JOIN marc AS c
    ON w~matnr = c~matnr
    INNER JOIN makt AS t
    ON c~matnr = t~matnr
    WHERE dismm = s_dismm
       AND ersda IN s_ersda
       AND stprs IN s_stprs.

  ENDIF.

  PERFORM loading.

  " 데이터 조회 후
  READ TABLE lt_itab INDEX 1. " 인터널 테이블을 읽었을 때
  IF sy-subrc NE 0. " 인터널 테이블이 비어있을 경우 = 조회값이 없을 경우
    MESSAGE '조회할 데이터가 없습니다.' TYPE 'I'. " 조회값이 없습니다 팝업 출력
    LEAVE LIST-PROCESSING. " 리스트 처리를 중단하고 빠져나옴(조회 페이지로 다시 감)
  ENDIF.

ENDFORM.                    "SELECT_DATA

* 로딩 게이지
FORM loading.
  DATA: g_total(10) TYPE n,
          g_cnt(10) TYPE n.

  DESCRIBE TABLE lt_itab LINES g_total.

  LOOP AT lt_itab.
    g_cnt = g_cnt + 1.
    PERFORM progress_indicator USING g_cnt g_total.
  ENDLOOP.

ENDFORM.                    "loading
*&---------------------------------------------------------------------*
*&      Form  progress_indicator
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->VALUE(P_CUR)    text
*      -->VALUE(P_TOTAL)  text
*----------------------------------------------------------------------*
FORM progress_indicator USING value(p_cur)
                            value(p_total).

  DATA : lv_text(50) TYPE c,
         lv_idx(3) TYPE n.

  SHIFT p_total LEFT DELETING LEADING '0'. " 숫자 왼쪽의 0 제거

  lv_idx = ( p_cur / p_total ) * 100.

  CONCATENATE  p_total '개의 데이터를 불러오는 중...' ' ' lv_idx ' %' INTO lv_text.

  CALL FUNCTION
    'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      percentage = lv_idx
      text       = lv_text
    EXCEPTIONS
      OTHERS     = 0.
ENDFORM.                    "progress_indicator
```

```abap
*&---------------------------------------------------------------------*
*&  Include           ZRRP_YH006ALV
*&---------------------------------------------------------------------*
TYPE-POOLS: slis. " slis 라이브러리 불러옴

* ALV 레이아웃에 사용되는 변수 선언
DATA  :  ls_layout        TYPE     slis_layout_alv,
         lt_fieldcat      TYPE     slis_t_fieldcat_alv, " 필드 카탈로그 선언
         ls_fieldcat      TYPE     slis_fieldcat_alv. " 필드 카탈로그 구조체 선언

* 사용할 인터널 테이블, 변수 초기화
FORM init_before_run.
  CLEAR    :  ls_layout, ls_fieldcat.
  REFRESH  :  lt_fieldcat,
              lt_itab.
ENDFORM.                    "INIT_BEFORE_RUN

*&---------------------------------------------------------------------*
*&      Form  set_layout
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM set_layout.
  ls_layout-zebra                 =   'X'. " ZEBRA 설정
  ls_layout-confirmation_prompt = 'X'. " ALV 리스트를 빠져나갈 때 확인 팝업창 오픈
ENDFORM.                    "set_layout

*&---------------------------------------------------------------------*
*&      Form  ALV_SETTING
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM   alv_setting.
  PERFORM set_layout." ALV 레이아웃 지정하는 서브루틴
  PERFORM set_fieldcat. " 필드 카탈로그 설정하는 서브루틴
ENDFORM.                    "ALV_SETTING

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_display.
  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING " 구조 정의
*      i_callback_program = sy-repid
      it_fieldcat        = lt_fieldcat " 필드 카탈로그 설정
      is_layout          = ls_layout " 레이아웃 설정
    TABLES " 들어갈 인터널 테이블 설정
      t_outtab           = lt_itab
    EXCEPTIONS
      program_error      = 1
      OTHERS             = 2.
ENDFORM.                    "ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  set_fieldcat
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM   set_fieldcat.

  PERFORM   fieldcat_assign       USING  :

     'WERKS' 'X' '플랜트명' 'LT_ITAB'  '10'  'L' '' '01',
     'ERSDA' 'X' '생성일' 'LT_ITAB'  '10'  'L' '' '02',
     'MATNR' 'X' '자재 번호' 'LT_ITAB'  '20'  'L' '' '03',
     'ERNAM' '' '오브젝트 생성자 이름' 'LT_ITAB'  '20'  'L' '' '04',
     'PSTAT' '' '유지보수상태' 'LT_ITAB'  '10'  'L' '' '05',
     'DISMM' '' 'MRP 유형' 'LT_ITAB'  '10'  'L' '' '06',
     'LBKUM' '' '총 평가 재고' 'LT_ITAB'  '20'  'L' 'KRW' '07',
     'SALK3' '' '평가 재고 총액' 'LT_ITAB'  '20'  'L' 'KRW' '08',
     'STPRS' ''  '표준가' 'LT_ITAB'  '20'  'L' 'KRW' '09',
     'MAKTX' ''  '자재명' 'LT_ITAB'  '30'  'L' '' '10'.
* 필드명, 키 여부, 딕셔너리의 텍스트 정보, 테이블명, 필드 길이, 텍스트 정렬 방향, 단위, 칼럼 순서

ENDFORM.                    "set_fieldcat

FORM    fieldcat_assign   USING   pa_fldnm " 필드명
                                  pa_key   " 키 여부
                                  pa_rddic  " 딕셔너리의 텍스트 정보를 나타냄
                                  pa_tabnm " 테이블 명
                                  pa_otlen " 필드 길이
                                  pa_just  " 텍스트 정렬 방향
                                  pa_meins " 단위
                                  pa_pos. " 칼럼 순서

  ls_fieldcat-fieldname       =   pa_fldnm.
  ls_fieldcat-key             =   pa_key.
  ls_fieldcat-reptext_ddic    =   pa_rddic.
  ls_fieldcat-tabname         =   pa_tabnm.
  ls_fieldcat-outputlen       =   pa_otlen.
  ls_fieldcat-just            =   pa_just.
  ls_fieldcat-qfieldname      =   pa_meins.
  ls_fieldcat-col_pos         =   pa_pos.

  APPEND ls_fieldcat TO lt_fieldcat.

ENDFORM.                    "fieldcat_assign
```

```abap
*&---------------------------------------------------------------------*
*& Report  ZRRP_YH006
*&
*&---------------------------------------------------------------------*
*&
*& 자재 조회 프로그램
*&---------------------------------------------------------------------*

REPORT  zrrp_yh006 NO STANDARD PAGE HEADING.

INCLUDE zrrp_yh006top.
INCLUDE zrrp_yh006src.
INCLUDE zrrp_yh006alv.

* 조회 결과 화면 출력
START-OF-SELECTION.

  PERFORM  run_main.

END-OF-SELECTION.

*&---------------------------------------------------------------------*
*&      Form  run_main
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM run_main.
  PERFORM init_before_run. " 사용할 인터널 테이블과 변수 초기화
  PERFORM select_data.     " DB에서 조건에 맞는 데이터 조회하여 인터널 테이블에 저장
  PERFORM alv_setting.     " ALV 세팅값 설정
  PERFORM alv_display.     " ALV 표시
ENDFORM.                    "RUN_MAIN
```